// Lorem ipsum dolor sit amet
// ==========================
//
//

Gabc
	= h:Header HeaderSeparator d:Description {return {"header":h,"words":d}}

HeaderSeparator
	= Newline? "%%" Newline+

Header
	= n:HeaderField+ {
    	const dict = new Object();
        n.forEach((element) => {
        	dict[element[0]] = element[1];
        })
        return dict;
      }
        	
    
HeaderFieldSeparator
	= (";;" / ";") _* Newline+

HeaderField
	= a:Key KeySeparator b:Value HeaderFieldSeparator {return [a,b]} 

KeySeparator
	= ":" _*

Key
	= n:Hchar+ {return n.join("")}

Value
	= n:Hchar+ {return n.join("")}

Hchar
	= Escape / [^%:;]

Description
	= n:Word+

Word
	= n:Syllable+ (_ / Newline)? {return {"syllables":n}}

Syllable
	= a:Text? b:Notes {return {"text":a,"notes":b}}

Text
	= a:[^ %()] b:Dchar* {return a.concat(b.join("").replace(/[{}]/g,""))}
    //removes {}

Notes
	= "(" n:(Clef / Note / Bar)* ")" {return n}

Note
	= a:Pitch b:Mod* {return {"pitch":a,"mods":b}}

Pitch
	= [a-mA-M]

Bar
	= n:("::" / (":" [1-6_']) / [:;,]) {return {"pitch":"bar","mods":[n]}}

Clef
	= n:("c1" / "c2" / "c3" / "c4" / "f2" / "f3" / "f4" / "cb3") {return {"pitch":"clef","mods":[n]}}

Mod
	= "sss" / "ss" / ".." / "vv" / Tweak / Escape / [^%():;,a-mA-M[\]]

Tweak
	= a:"[" b:(Escape / [^%()[\]])* c:"]" {return}

Dchar
	= Escape / [^%()]

_ "whitespace"
	= " "+

Newline
	= [\t\r\n]+

Escape
	= "$" n:[^] {return n}